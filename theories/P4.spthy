theory P4 
begin

builtins: hashing, symmetric-encryption

// Set up symmetric keys
rule SetupSymmetricKey:
  [ Fr(~k) ] --[ InitKey() ]-> [ !Ltk($A, $B, ~k) ]

rule Reveal_ltk:
  [ !Ltk(A, B, k) ] --[ Reveal(A), Reveal(B) ]-> [ Out(k) ]

// user-defined function
functions: mac/2, kdf/3
  
/*
  We model the following protocol

  1. A -> B: x, enc{s}h(kIR)
  2. A <- B: y
  3. A -> B: sign{<'I',y>}sIR
  4. A <- B: sign{<'R',x>}sIR
 */

rule Init_A:
  [ Fr(~id)
  , !Ltk(I, R, kIR)
  ]
--[ Init_A()
  , Create_I(I, ~id) 
  ]->
  [ St_I_1(I, ~id, R, kIR)
  ]

rule Init_B:
  [ Fr(~id)
  , !Ltk(I, R, kIR)
  ]
--[ Init_B()
  , Create_R(R, ~id)
  ]->
  [ St_R_1(R, ~id, I, kIR)
  ]


// 1. A -> B: x, enc{s}h(kIR)

rule I_1:
  let m = senc{~s}h(kIR)
  in
  [ St_I_1(I, id, R, kIR)
  , Fr(~x)
  , Fr(~s)
  ]
--[ Send(I,<~x,m>)
  ]->
  [ St_I_2(I, id, R, kIR, ~x, ~s)
  , Out(<~x,m>)
  ]

rule R_1:
  let s = sdec{m}h(kIR)
  in
  [ St_R_1(R, id, I, kIR)
  , In(<x,m>)
  ]
--[ Receive(R,<x,m>)
  ]->
  [ St_R_2(R, id, I, kIR, x, s)
  ]

// 2. A <- B: y

rule R_2:
  let sIR = kdf(s, x, ~y)
  in
  [ St_R_2(R, id, I, kIR, x, s)
  , Fr(~y)
  ]
--[ Send(R,~y)
  , Running( R, I, <'I', 'R', x, ~y, sIR>)
  ]->
  [ St_R_3(R, id, I, kIR, x, s, ~y)
  , Out(~y)
  ]

rule I_2:
  let sIR = kdf(s, x, y)
  in
  [ St_I_2(I, id, R, kIR, x, s)
  , In(y)
  ]
--[ Receive(I,y)
  , Running( I, R, <'R', 'I', x, y, sIR> )
  ]->
  [ St_I_3(I, id, R, kIR, x, s, y)
  ]
  
  
// 3. A -> B: {y}_{sIR}

rule I_3:
  let sIR = kdf(s, x, y)
      m   = mac{<'I',y>}sIR
  in
  [ St_I_3(I, id, R, kIR, x, s, y)
  ]
--[ Send(I, m)
  ]->
  [ St_I_4(I, id, R, kIR, x, s, y, sIR)
  , Out(m)
  ]

rule R_3:
  let sIR = kdf(s, x, y)
      m   = mac{<'I',y>}sIR
  in
  [ St_R_3(R, id, I, kIR, x, s, y)
  , In(m)
  ]
--[ Receive(R,m)
  ]->
  [ St_R_4(R, id, I, kIR, x, s, y, sIR)
  ]

// 4. A <- B: {x}_{sIR}

rule R_4:
  let m = mac{<'R',x>}sIR
  in
  [ St_R_4(R, id, I, kIR, x, s, y, sIR)
  ]
--[ Send(R,m)
  , Commit(R,I,<'R','I',x,y,sIR>)
  , Secret(R, sIR)
  , FinishR()
  , Honest(I)
  , Honest(R)
  ]->
  [ St_R_5(R, id, I, kIR, x, s, y, sIR)
  , Out(m)
  ]
  
rule I_4:
  let m = mac{<'R',x>}sIR
  in
  [ St_I_4(I, id, R, kIR, x, s, y, sIR)
  , In(m)
  ]
--[ Receive(I,m)
  , Commit(I,R,<'I','R',x,y,sIR>) 
  , Secret(I, sIR)
  , FinishI()
  , Honest(I)
  , Honest(R)
  ]->
  [ St_I_5(I, id, R, kIR, x, s, y, sIR)
  ]

// Own lemma
lemma executable:
  exists-trace
    "Ex #i #j. FinishI()@i & FinishR()@j 
   & not (Ex A #j. Reveal(A)@j)
   & (All #j1 #j2. InitKey()@j1 & InitKey()@j2 ==> #j1 = #j2)
   & (All #j1 #j2. Init_A()@j1 & Init_A()@j2 ==> #j1 = #j2)
   & (All #j1 #j2. Init_B()@j1 & Init_B()@j2 ==> #j1 = #j2)
   & (Ex A B idA idB #i #j. Create_I(A,idA)@i & Create_R(B,idB)@j & not A=B)
   "


// Given, immutable lemmas

lemma secrecy:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma secrecy_PFS:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i & r < i)"


lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


lemma noninjectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

end
